/*

 This build script will build the dependency jar files required to implement a
 plugin.  Invoke it with the command:

 $ gradle -b libraries.gradle --rerun-tasks buildAllLibraries

 The output jars will be placed in build/libs.

*/

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        baseAPI {}
        locationAPI {}
    }

    sourceSets {
        // The default "main" configuration.
        main {
            // Exclude all source files; do not compile anything.
            java {
                exclude '**'
            }

            aidl {
                exclude '**'
            }
        }

        // The configuration to build the base API.
        baseAPI {
            setRoot 'src/main'

            java {
                exclude '**'
            }
        }
    }
}

dependencies {
    // Dependencies for the locationAPI configuration.
    locationAPICompileOnly fileTree(dir: 'build/libs/', include: ['PermissionsPluginLibrary-baseAPI.jar'])
}

// Assemble the JAR file for the base API.
task assembleBaseAPIJar(type: Jar, dependsOn: "assembleBaseAPI") {
    baseName  "PermissionsPluginLibrary-baseAPI"
    from "$buildDir/intermediates/classes/baseAPI/"
    exclude '**/BuildConfig.class'
    exclude '**/R.class'
    exclude '**/R$*.class'
}

// Leaf task that will build all the libraries.
task buildAllLibraries (dependsOn: ["assembleBaseAPIJar"]) {
    // This task is intentionally left empty.
}
